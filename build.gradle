apply plugin: 'base'
apply plugin: 'java'

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.13.1'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.8'
    }
}

group = 'com.gluonhq'
version = '4.0.0-SNAPSHOT'

def currentOS = org.gradle.internal.os.OperatingSystem.current();
def classifier
if (currentOS.isWindows()) {
    classifier = 'win'
} else if (currentOS.isLinux()) {
    classifier = 'linux'
} else if (currentOS.isMacOsX()) {
    classifier = 'mac'
}

subprojects {
    repositories {
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url "https://oss.sonatype.org/content/repositories/releases"
        }
        jcenter()
        mavenCentral()
    }

    if (project.name.endsWith("/android")) {
        File androidMavenRepository = project.file("$ANDROID_HOME/extras/android/m2repository")
        if (! androidMavenRepository.exists()) {
            throw new GradleException("Unable to find Android repository at: " + androidMavenRepository)
        }

        File googleMavenRepository = project.file("$ANDROID_HOME/extras/google/m2repository")
        if (! googleMavenRepository.exists()) {
            throw new GradleException("Unable to find Google repository at: " + googleMavenRepository)
        }
        repositories {
            maven {
                url androidMavenRepository.toURI().toString()
            }
            maven {
                url googleMavenRepository.toURI().toString()
            }
        }
    }

    apply plugin: 'java'
    apply plugin: 'maven'
    apply from: rootProject.file('gradle/code-quality.gradle')
    apply plugin: 'com.github.ben-manes.versions'
    apply from: rootProject.file('mavenPublish.gradle')
    
    dependencies {
        compile "org.openjfx:javafx-controls:11-ea+25:$classifier"
        compile "org.openjfx:javafx-graphics:11-ea+25:$classifier"
        compile "org.openjfx:javafx-base:11-ea+25:$classifier"
    }

    if (project.name.endsWith("/ios")) {
        apply from: rootProject.file('gradle/ios-build.gradle')
    }
    
    group = rootProject.group
    version = rootProject.version
    archivesBaseName = 'charm-down-' + name.replaceAll("/", "-")

    configurations {
        published
    }

    if (project.name.endsWith("/android") && ! project.name.contains("core")) {
        dependencies {
            compile project(':core/android')
        }
    }
    jar {
        eachFile {
            if ((it.name.endsWith('.class')) && !(it.name.equals('module-info.class') ))  {
                RandomAccessFile raf = new RandomAccessFile(it.file.path, "rw");
                raf.seek(7);
                raf.writeByte(0x34); //make it jdk8
                raf.close();
            }
        }
    }


/*
    task sourceJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        from javadoc.destinationDir
        classifier = 'javadoc'
    }

    artifacts {
        archives sourceJar, javadocJar
    }
*/
}

task allJavadoc(type: Javadoc) {
    source subprojects.collect { project ->
        project.sourceSets.main.allJava.matching {
            exclude 'com/gluonhq/impl/**/*'
        }
    }
    classpath = files(subprojects.collect { project ->
        project.sourceSets.main.compileClasspath
    })
}
javadoc.finalizedBy allJavadoc
